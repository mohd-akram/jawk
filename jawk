#!/bin/sh

set -eu

name=`basename $0`

usage() {
	echo "usage: $name [-v var=value] [-f progfile | 'prog'] [file]" >&2
	exit 1
}

p=
prog=
while getopts :v:f: opt; do
	case $opt in
	f) prog="$prog`cat "$OPTARG"`"; p=1 ;;
	v) ;;
	?) usage
	esac
done

i=0
skip=
f=
file=
for arg do
	i=$((i+1))
	shift
	if [ "$i" -lt "$OPTIND" ]; then
		if [ "$skip" ]; then skip=; continue; fi
		if [ "$arg" != "${arg#-f}" ]; then
			if [ "$arg" = "-f" ]; then skip=1; fi
			continue
		fi
	else
		if [ ! "$p" ]; then prog=$arg; p=1
		elif [ ! "$f" ]; then file=$arg; f=1
		else usage; fi
		continue
	fi
	set -- "$@" "$arg"
done

if [ ! "$p" ]; then usage; fi
if [ ! "$f" ]; then file=-; fi

cbs='

function cb_parse_array_empty(jpath) {

	return "[]"
}

function cb_parse_object_empty(jpath) {

	return "{}"
}

function cb_parse_array_enter(jpath) {

	if ("" != jpath)
		;
}

function cb_parse_array_exit(jpath, status) {

}

function cb_parse_object_enter(jpath) {

	if ("" != jpath)
		;
}

function cb_parse_object_exit(jpath, status) {

}

function cb_append_jpath_component (jpath, component) {
	if (match(component, /^[0-9]+$/))
		component++;
	if (component == "") return jpath
	gsub(/^"|"$/, "", component)
	if (jpath == "") return "\"" component "\""
	gsub(/^"|"$/, "", jpath)
	return "\"" jpath SUBSEP component "\""
}

function cb_append_jpath_value (jpath, value) {

	return sprintf("%s\t%s", jpath, value)
}

function cb_jpaths (ary, size,   i) {
	for(i=1; i <= size; i++) {
		print ary[i]
	}

	print ""
}

function cb_fails (ary, size,   k) {

	for(k in ary) {
		print "cb_fails: invalid input file:", k
		print FAILS[k]
	}
}

function cb_fail1 (message) {

	print "cb_fail1: invalid input file:", FILENAME
	print message
}
'
parser='
#!/usr/bin/awk -f
#


BEGIN { #{{{1
	if (BRIEF  == "") BRIEF=1  # when 1 parse() omits non-leaf nodes from stdout
	if (STREAM == "") STREAM=1 # when 0 parse() stores JPATHS[] for callback cb_jpaths

	NO_EMPTY_STR = 0; NO_EMPTY_ARY = NO_EMPTY_OBJ = 1

	if (BRIEF > 0) { # parse() will look at NO_EMPTY_*
		NO_EMPTY_STR = !(x=bit_on(BRIEF, 0))
		NO_EMPTY_ARY = !(x=bit_on(BRIEF, 1))
		NO_EMPTY_OBJ = !(x=bit_on(BRIEF, 2))
		if (x=bit_on(BRIEF, 3)) NO_EMPTY_STR = 1 # wins over bit 0
	}

	delete FAILS
	reset()

	if (1 == ARGC) {
		while (getline ARGV[++ARGC] < "/dev/stdin") {
			if (ARGV[ARGC] == "")
				break
		}
	} # else usage: awk -f JSON.awk file1 [file2...]

	srand(); RS="\1" rand()
}

{ # main loop: process each file in turn {{{1
	reset() # See important application note in reset()

	++FILEINDEX # 1-based
	tokenize($0) # while(get_token()) {print TOKEN}
	while (0 == parse()) {
		if (0 == STREAM)
			cb_jpaths(JPATHS, NJPATHS)
		else
			print ""
	}
}

END { # process invalid files {{{1
	if (0 == STREAM) {
		cb_fails(FAILS, NFAILS)
	}
}

function bit_on(n, b) { #{{{1
	if (b == 0) return n % 2
	return int(n / 2^b) % 2
}

function append_jpath_component(jpath, component) { #{{{1
	if (0 == STREAM) {
		return cb_append_jpath_component(jpath, component)
	} else {
		return (jpath != "" ? jpath "," : "") component
	}
}

function append_jpath_value(jpath, value) { #{{{1
	if (0 == STREAM) {
		return cb_append_jpath_value(jpath, value)
	} else {
		return sprintf("[%s]\t%s", jpath, value)
	}
}

function get_token() { #{{{1


	TOKEN = TOKENS[++ITOKENS] # for internal tokenize()
	return ITOKENS < NTOKENS
}

function parse_array_empty(jpath) { #{{{1
	if (0 == STREAM) {
		return cb_parse_array_empty(jpath)
	}
	return "[]"
}

function parse_array_enter(jpath) { #{{{1
	if (0 == STREAM) {
		cb_parse_array_enter(jpath)
	}
}

function parse_array_exit(jpath, status) { #{{{1
	if (0 == STREAM) {
		cb_parse_array_exit(jpath, status)
	}
}

function parse_array(a1,   idx,ary,jpath,ret,x) { #{{{1
	idx=0
	ary=""
	get_token()
	if (TOKEN != "]") {
		while (1) {
			if (ret = parse_value(a1, idx)) {
				return ret
			}
			idx=idx+1
			ary=ary VALUE
			get_token()
			if (TOKEN == "]") {
				break
			} else if (TOKEN == ",") {
				ary = ary ","
			} else {
				report(", or ]", TOKEN ? TOKEN : "EOF")
				return 2
			}
			get_token()
		}
		CB_VALUE = sprintf("[%s]", ary)
		VALUE = 0 == BRIEF ? CB_VALUE : ""
	} else {
		VALUE = CB_VALUE = parse_array_empty(a1)
	}

	jpath = append_jpath_component(a1, "\"length\"")
	x = append_jpath_value(jpath, idx)
	if(0 == STREAM) {
		JPATHS[++NJPATHS] = x
	} else {
		print x
	}

	return 0
}

function parse_object_empty(jpath) { #{{{1
	if (0 == STREAM) {
		return cb_parse_object_empty(jpath)
	}
	return "{}"
}

function parse_object_enter(jpath) { #{{{1
	if (0 == STREAM) {
		cb_parse_object_enter(jpath)
	}
}

function parse_object_exit(jpath, status) { #{{{1
	if (0 == STREAM) {
		cb_parse_object_exit(jpath, status)
	}
}

function parse_object(a1,   key,obj,keys,key_) { #{{{1
	obj=""
	get_token()
	if (TOKEN != "}") {
		while (1) {
			if (TOKEN ~ /^".*"$/) {
				key=TOKEN
			} else {
				report("string", TOKEN ? TOKEN : "EOF")
				return 3
			}
			get_token()
			if (TOKEN != ":") {
				report(":", TOKEN ? TOKEN : "EOF")
				return 4
			}
			get_token()
			if (parse_value(a1, key)) {
				return 5
			}
			key_ = key
			gsub(/^"|"$/, "", key_)
			keys=(keys ? keys "\037" : "") key_
			obj=obj key ":" VALUE
			get_token()
			if (TOKEN == "}") {
				break
			} else if (TOKEN == ",") {
				obj=obj ","
			} else {
				report(", or }", TOKEN ? TOKEN : "EOF")
				return 6
			}
			get_token()
		}
		CB_VALUE = sprintf("{%s}", obj)
		VALUE = 0 == BRIEF ? CB_VALUE : ""
	} else {
		VALUE = CB_VALUE = parse_object_empty(a1)
	}
	jpath = append_jpath_component(a1, KEYS)
	x = append_jpath_value(jpath, keys)
	if(0 == STREAM) {
		JPATHS[++NJPATHS] = x
	} else {
		print x
	}
	return 0
}

function parse_value(a1, a2,   jpath,ret,x) { #{{{1
	jpath = append_jpath_component(a1, a2)

	if (TOKEN == "{") {
		parse_object_enter(jpath)
		if (parse_object(jpath)) {
			parse_object_exit(jpath, 7)
			return 7
		}
		parse_object_exit(jpath, 0)
	} else if (TOKEN == "[") {
		parse_array_enter(jpath)
		if (ret = parse_array(jpath)) {
			parse_array_exit(jpath, ret)
			return ret
		}
		parse_array_exit(jpath, 0)
	} else if (TOKEN == "") { #test case 20150410 #4
		report("value", "EOF")
		return 9
	} else if (TOKEN ~ /^([^0-9])$/) {
		report("value", TOKEN)
		return 9
	} else {
		CB_VALUE = VALUE = TOKEN
	}

	if (0 < BRIEF && ("" == jpath || "" == VALUE)) {
		return 0
	}

	if (0 < BRIEF && (NO_EMPTY_STR && VALUE=="\"\"" || NO_EMPTY_ARY && VALUE=="[]" || NO_EMPTY_OBJ && VALUE=="{}")) {
		return 0
	}

	x = append_jpath_value(jpath, VALUE)
	if(0 == STREAM) {
		JPATHS[++NJPATHS] = x
	} else {
		print x
	}
	return 0
}

function parse(   ret) { #{{{1
	get_token()
	if (!TOKEN) return 1;
	if (ret = parse_value()) {
		return ret
	}
	return 0
}

function report(expected, got,   i,from,to,context) { #{{{1
	from = ITOKENS - 10; if (from < 1) from = 1
	to = ITOKENS + 10; if (to > NTOKENS) to = NTOKENS
	for (i = from; i < ITOKENS; i++)
		context = context sprintf("%s ", TOKENS[i])
	context = context "<<" got ">> "
	for (i = ITOKENS + 1; i <= to; i++)
		context = context sprintf("%s ", TOKENS[i])
	scream("expected <" expected "> but got <" got "> at input token " ITOKENS "\n" context)
}

function reset() { #{{{1

	TOKEN=""; delete TOKENS; NTOKENS=ITOKENS=0
	delete JPATHS; NJPATHS=0
	CB_VALUE = VALUE = ""
}

function scream(msg) { #{{{1
	NFAILS += (FILENAME in FAILS ? 0 : 1)
	FAILS[FILENAME] = FAILS[FILENAME] (FAILS[FILENAME]!="" ? "\n" : "") msg
	if(0 == STREAM) {
		if(cb_fail1(msg)) {
			print FILENAME ": " msg >"/dev/stderr"
		}
	} else {
		print FILENAME ": " msg >"/dev/stderr"
	}
}

function tokenize(a1,   pq,pb,ESCAPE,CHAR,STRING,NUMBER,KEYWORD,SPACE) { #{{{1

	SPACE="[[:space:]]+"
	gsub(/(^\xEF\xBB\xBF)|\"[^\"\\[:cntrl:]]*((\\[^u[:cntrl:]]|\\u[0-9a-fA-F]{4})[^\"\\[:cntrl:]]*)*\"|-?(0|[1-9][0-9]*)([.][0-9]*)?([eE][+-]?[0-9]*)?|null|false|true|[[:space:]]+|./, "\n&", a1)
	gsub("\n" SPACE, "\n", a1)
	sub(/^\n(\xEF\xBB\xBF\n)?/, "", a1)
	ITOKENS=0 # get_token() helper
	return NTOKENS = split(a1, TOKENS, /\n/)
}

'
jawk='
BEGIN { RS=""; FS="\n"; JSON="\1"; TYPE="\2" }
function keys(ks, o, n, a, i) {
	if (o == "" && o == 0) {
		if ("\3" in _) {
			n = split(_["\3"], a, "\037")
			while (++i <= n) ks[a[i]] = a[i]
		} else {
			n = _["length"]
			while (++i <= _["length"]) ks[i] = i
		}
	}
	else {
		if ((o SUBSEP "\3") in _) {
			n = split(_[o,"\3"], a, "\037")
			while (++i <= n) ks[a[i]] = o SUBSEP a[i]
		} else {
			n = _[o,"length"]
			while (++i <= n) ks[i] = o SUBSEP i
		}
	}
	return n
}
function __jawk(i, kv, key, value, raw_value, type, arg, cmd, ret, j) {
	for (i = 1; i <= NF; i++) {
		split($i, kv, "\t")
		key = substr(kv[1], 2, length(kv[1]) - 2)
		value = kv[2]
		raw_value = value
		if (match(value, /^"/)) {
			value = substr(value, 2, length(value) - 2)
			if (match(value, /\\/)) {
				gsub(/\\"/, "\"", value)
				gsub(/\\n/, "\n", value)
				gsub(/\\r/, "\r", value)
				gsub(/\\t/, "\t", value)
				if (match(value, /\\/)) {
					arg = raw_value
					gsub(/\\/, "\\\\", arg)
					gsub(/\\"/, "\"", arg)
					gsub(/`/, "\\`", arg)
					gsub(/\$/, "\\$", arg)
					cmd="printf %b "arg
					value=""
					RS="\n"
					while (ret = cmd | getline v) {
						if (ret == -1)
							exit 1
						++j
						if (j == 1)
							value = v
						else
							value = value "\n" v
					}
					close(cmd)
					RS=""
					if (match(raw_value, /(^|[^\\])(\\\\)*\\n"$/))
						value = value "\n"
				}
			}
			type = "string"
		}
		else if (match(value, /^\{/)) {
			value = key
			type = "object"
		}
		else if (match(value, /^\[/)) {
			value = key
			type = "array"
		}
		else if (value == "true") {
			value = 1
			type = "boolean"
		}
		else if (value == "false") {
			value = 0
			type = "boolean"
		}
		else if (value == "null") {
			value = ""
			type = "null"
		} else {
			type = "number"
		}
		if (kv[1]) {
			_[key] = value
			_[key,JSON] = raw_value
			_[key,TYPE] = type
		} else {
			if (type != "array" && type != "object")
				_[0] = value;
			_[JSON] = raw_value
			_[TYPE] = type
		}
	}
}
{ __jawk() }
'

awk -v BRIEF=0 -v STREAM=0 -v KEYS="\3" "$cbs""$parser" "$file" | \
awk "$@" "$jawk""$prog"
